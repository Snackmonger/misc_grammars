%import common.C_COMMENT
// This is a simple grammar to parse basic chord forms. 
//
// The interpreter needs to make some inferences beyond the grammar's syntax 
// (e.g. Cmaj implies a p5, C13 implies a 9 and 11, etc.), but that is not the
// parser's responsibility.
start: chordform
     | slash_chord

chordform: normal_chord altered_extension* add* no*
slash_chord: chordform "/" bass 
bass: _note_name
error: _roman_name (_maj | _min | _dim | _aug | _halfdim | main_extension | altered_extension | add | no)* "/"
normal_chord: major 
            | minor 
            | suspended
            | diminished 
            | augmented 
            | half_diminished
            | dominant

major: root_symbol SIX?
     | root_symbol _maj SIX?
     | root_symbol maj_extension
minor: root_symbol _min (main_extension | maj_extension | SIX)?
suspended: root_symbol (main_extension | maj_extension | SIX)? sus
diminished: root_symbol _dim (main_extension | maj_extension)?
          | root_symbol _min (main_extension | maj_extension)? "b5" 
augmented: root_symbol _aug (main_extension | maj_extension)?
         | root_symbol (main_extension | maj_extension | SIX) (_aug | "#5")
half_diminished: root_symbol _min main_extension "b5"
               | root_symbol _halfdim main_extension?
dominant: root_symbol main_extension

root_symbol: _note_name | _roman_name
_note_name: _letter_name _accidental*
_roman_name: _accidental* roman_numeral

main_extension: (SEVEN | NINE | ELEVEN | THIRTEEN)
maj_extension: _maj (SEVEN | NINE | ELEVEN | THIRTEEN)
sus: "sus" (TWO | FOUR | "bb" THREE | "#" THREE) 
altered_extension: _accidental+ NUMBER
add: "add" _accidental* NUMBER
no: "no" _accidental* NUMBER
_accidental: SHARP
          | FLAT
SHARP: "#"
FLAT: "b"
NUMBER: TWO
      | THREE
      | FOUR
      | FIVE
      | SIX
      | SEVEN
      | NINE
      | ELEVEN
      | THIRTEEN
TWO: "2"
THREE: "3"
FOUR: "4"
FIVE: "5"
SIX: "6"
SEVEN: "7"
NINE: "9"
ELEVEN: "11"
THIRTEEN: "13"
_letter_name: A
           | B
           | C
           | D
           | E 
           | F
           | G
A: "A"
B: "B"
C: "C"
D: "D"
E: "E"
F: "F"
G: "G"
roman_numeral: I | II | III | IV | V | VI | VII
I: "I" | "i"
II: "II" | "ii"
III: "III" | "iii"
IV: "IV" | "iv"
V: "V" | "v"
VI: "VI" | "vi"
VII: "VII" | "vii"

_maj: _MAJ
   | _M_UPPER
   | _DELTA
_min: _MIN
   | _M_LOWER
   | _MINUS
_dim: _DIM
_halfdim: _OE
_aug: _AUG
   | _PLUS
_M_UPPER: "M"
_MAJ: "maj"
_DELTA: "Δ"
_MIN: "min"
_M_LOWER: "m"
_MINUS: "-"
_DIM: "dim"
    | "o"
_OE: "ø"
_AUG: "aug"
_PLUS: "+"